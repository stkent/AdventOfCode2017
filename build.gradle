buildscript {
    ext.kotlin_version      = '1.2.10'
    ext.kotlin_test_version = '2.0.7'
    ext.jvm_target_version  = '1.8'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

subprojects {
    // Configuration common to all subprojects ---------------------------------------------------------------------- //

    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        testCompile "io.kotlintest:kotlintest:$kotlin_test_version"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "$jvm_target_version"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "$jvm_target_version"
    }

    // Configuration common to all daily solution subprojects ------------------------------------------------------- //

    if (name.startsWith('day')) {
        // Make the project runnable (per https://docs.gradle.org/current/userguide/application_plugin.html):
        apply plugin: 'application'
        mainClassName = "Main"

        // Expose utilities to the project:
        dependencies.compile project(":utilities")
    }
}
